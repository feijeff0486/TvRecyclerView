apply plugin: 'com.android.library'

// 2. 添加这两行
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {

    compileSdkVersion 25
    buildToolsVersion "26.0.1"

    aaptOptions {
        cruncherEnabled = true
        useNewCruncher = false
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.gogh:rxokretrofit:1.0.02'
    compile 'com.android.support:recyclerview-v7:25.3.0'
}

//项目主页
def siteUrl = 'https://bintray.com/goghtsui/ExpandView'
//项目的git地址
def gitUrl = 'git@github.com:goghtsui/TvRecyclerView.git'
// 上传到 Bintray 的 package 名称
def libName = "ExpandView"
// 这两个参数配置是为了最终生成 compile 'com.gogh:依赖库的名称:1.0.0'  group  version 是关键字，自动识别的
group = "com.gogh";
version = "1.10.19"

install {
    repositories.mavenInstaller {
        // 生成pom.xml和参数
        pom {
            project {
                packaging 'aar'
                // 可选，项目名称
                name 'ExpandView'
                // 可选，项目描述
                description 'Support for http request, use okhttp rxjava and retrofit. Add log info module.'
                url siteUrl // 项目主页，这里是引用上面定义好

                // 软件开源协议，现在一般都是Apache License2.0吧，不需要修改
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                //填写开发者基本信息，需要修改
                developers {
                    developer {
                        id 'gaoxiaofeng' // 开发者的id
                        name 'gaoxiaofeng' // 开发者名字
                        email 'xiaofeng355@gmail.com' // 开发者邮箱
                    }
                }

                // SCM，不需要修改
                scm {
                    connection gitUrl // Git仓库地址
                    developerConnection gitUrl // Git仓库地址
                    url siteUrl // 项目主页
                }
            }
        }
    }
}

//上传到JCenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.username")    // 读取 local.properties 文件里面的 bintray.user 登录用户名
    key = properties.getProperty("bintray.apikey")   // 读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        //这里的repo值必须要和你创建Maven仓库的时候的名字一样
        repo = "ExpandView"
        //发布到JCenter上的项目名字
        name = libName
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true //是否是公开项目
    }
}

// 生成jar包的task，不需要修改
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
// 生成jarDoc的task，不需要修改
task javadoc(type: Javadoc) {
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // destinationDir = file("../javadoc/")
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过
}
// 生成javaDoc的jar，不需要修改。
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

